(ns mongol-drafts.widgets.button-bar
  (:require
   ["package:flutter/material.dart" :as m]
   [cljd.flutter :as f]
   [mongol-drafts.widgets.profile :as profile]
   [mongol-drafts.states.global :as global-state]
   [mongol-drafts.states.draft :as draft-state]
   [mongol-drafts.states.tag :as tag-state]
   [mongol-drafts.utils.modal :as modal]
   [virtual-keyboard.keyboard-action :as keyboard-action]))
   
(defn bar-button [child on-press]
  (f/widget
   :context ctx
   (m/Material
    .color (-> m/Theme (.of ctx) .-scaffoldBackgroundColor)
    .child
    (m/InkWell
     .customBorder (m/CircleBorder)
     .onTap on-press
     .child
     (m/Padding
      .padding (.all m/EdgeInsets 2)
      .child child)))))

(defn topbar [controller]
  (f/widget
   :context ctx
   :watch [{char-count :editor/chars
            word-count :editor/words}
           global-state/state]
   (m/Row
    .mainAxisAlignment m.MainAxisAlignment/spaceAround
    .children
    [(bar-button
      (m/Icon m/Icons.article_outlined)
      (fn []
        (-> m/Scaffold (.of ctx) (.openDrawer))))
     (bar-button
      (m/Icon m/Icons.add)
      (fn []
        (await (draft-state/new-draft))
        (draft-state/focus-on-draft controller)))
     (bar-button
      (m/Icon m/Icons.sell_outlined)
      (fn []
        (swap! global-state/state
               assoc
               :tag-viewable
               (not (:tag-viewable @global-state/state)))
        (tag-state/get-draft-tags global-state/state (-> @global-state/state :draft (get "id")))))
     (m/SizedBox .width 20)
     (m/Text
      (str char-count "c" word-count "w"))
     (bar-button
      (m/Icon m/Icons.arrow_drop_down)
      (fn [] nil))
     (bar-button
      (m/Icon m/Icons.search)
      (fn []
        (swap! global-state/state assoc :show-search-dialog true)
        (swap! global-state/state assoc :show-stacked-keyboard true)
        (swap! global-state/state assoc :show-keyboard false)))
     (bar-button
      (m/Icon m/Icons.view_sidebar_outlined)
      (fn []
        (modal/modal-bottom-sheet ctx (profile/widget))
        (swap! global-state/state assoc :show-search-dialog false)))])))



(defn keyboard-close-button [flag]
  (f/widget
   (m/Container
    .height 40
    .child
    (m/GestureDetector
     .child (m/Icon m/Icons.keyboard_arrow_down)
     .onTap (fn []
              (keyboard-action/clear-candidates)
              (if (true? flag)
                (swap! global-state/state
                       assoc
                       :show-keyboard
                       (not (:show-keyboard @global-state/state)))
                (swap! global-state/state
                       assoc
                       :show-stacked-keyboard
                       (not (:show-stacked-keyboard @global-state/state)))))))))


;;;
(defn keyboard-upper-bar []
  (m/Expanded
   .child
   (m/Row
    .children
    [(keyboard-close-button false)])))

(defn sentence-editor-bar [save-fn]
  (f/widget
   :context ctx
   (m/Row
    .mainAxisSize m.MainAxisSize/max
    .mainAxisAlignment m.MainAxisAlignment/spaceBetween
    .children
    [(bar-button
      (m/Icon m/Icons.arrow_back)
      (fn []
        (-> m/Navigator (.of ctx) .pop)))
     (bar-button
      (m/Icon m/Icons.share)
      save-fn)])))