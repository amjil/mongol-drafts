(ns mongol-drafts.widgets.profile
  (:require
   ["package:flutter/material.dart" :as m]
   [cljd.flutter :as f]
   [mongol-drafts.widgets.settings :as settings]
   [mongol-drafts.widgets.theme-dialog :as theme-dialog]
   [mongol-drafts.widgets.font-dialog :as font-dialog]
   [mongol-drafts.widgets.font-size-dialog :as font-size-dialog]
   [mongol-drafts.services.update-check :as update-check]
   [mongol-drafts.services.feedback :as feedback]
   [mongol-drafts.states.theme :as theme-state]
   [mongol-drafts.states.font :as font-state]
   [mongol-drafts.states.global :as global-state]))
   
(defn- get-theme-subtitle [theme-mode]
  (condp = theme-mode
    theme-state/theme-mode-light "ᠴᠠᠭᠠᠨ "
    theme-state/theme-mode-dark "ᠬᠠᠷᠠᠪᠲᠤᠷ "
    theme-state/theme-mode-system "ᠰᠢᠰᠲ᠋ᠧᠮ ᠎ᠢ ᠳᠠᠭᠠᠬᠤ"
    "ᠰᠢᠰᠲ᠋ᠧᠮ ᠎ᠢ ᠳᠠᠭᠠᠬᠤ"))

(defn profile-view []
  (f/widget
   :context ctx
   :watch [{current-mode :mode} theme-state/theme-state
           {app-font :app-font draft-font :draft-font
            app-font-size :app-font-size draft-font-size :draft-font-size} font-state/font-state]
   :let [theme-subtitle (get-theme-subtitle current-mode)]
   (m/SingleChildScrollView .scrollDirection m.Axis/horizontal)
   (m/Row
    .crossAxisAlignment m.CrossAxisAlignment/start
    .children
    [(settings/settings-group
      [{:title "ᠳᠡᠯᠭᠡᠴᠡ ᠎ᠶᠢᠨ ᠰᠡᠳᠦᠪ "
        :leading m/Icons.palette
        :subtitle (str "ᠣᠳᠣ : " theme-subtitle)
        :on-tap (fn [] (theme-dialog/show-theme-dialog ctx))}
       {:title "ᠠᠫᠫ ᠹᠦᠨᠲ "
        :leading m/Icons.font_download
        :subtitle (str "ᠣᠳᠣ : " app-font)
        :on-tap (fn [] (font-dialog/show-app-font-dialog ctx))}
       {:title "ᠠᠫᠫ ᠹᠦᠨᠲ ᠎ᠶᠢᠨ ᠬᠡᠮᠵᠢᠶ᠎ᠡ "
        :leading m/Icons.format_size
        :subtitle (str "ᠣᠳᠣ : " app-font-size)
        :on-tap (fn [] (font-size-dialog/show-app-font-size-dialog ctx))}
       {:title "ᠲᠡᠮᠳᠡᠭᠯᠡᠯ ᠎ᠦᠨ ᠹᠦᠨᠲ "
        :leading m/Icons.article
        :subtitle (str "ᠣᠳᠣ : " draft-font)
        :on-tap (fn [] (font-dialog/show-draft-font-dialog ctx))}
       {:title "ᠲᠡᠮᠳᠡᠭᠯᠡᠯ ᠎ᠦᠨ ᠹᠦᠨᠲ ᠎ᠶᠢᠨ ᠬᠡᠮᠵᠢᠶ᠎ᠡ "
        :leading m/Icons.text_fields
        :subtitle (str "ᠣᠳᠣ : " draft-font-size)
        :on-tap (fn [] (font-size-dialog/show-draft-font-size-dialog ctx))}
       {:title "ᠰᠢᠨᠡᠴᠢᠯᠡᠯᠲᠡ᠎ᠶᠢ ᠰᠢᠯᠭᠠᠬᠤ"
        :leading m/Icons.system_update
        :subtitle (str "Version: " @global-state/build-number)
        :on-tap (fn [] (update-check/check-for-update! ctx {:manual? true}))}
       {:title "ᠰᠠᠨᠠᠯ ᠵᠥᠪᠯᠡᠯᠭᠡ "
        :leading m/Icons.feedback
        :subtitle "ᠲᠤᠰᠠᠯᠠᠮᠵᠢ ᠪᠠ ᠰᠠᠨᠠᠯ"
        :on-tap (fn [] (feedback/open-feedback-dialog! ctx))}])])))

(defn default-view []
  (f/widget
   :context ctx
   (m/Padding
    .padding (m/EdgeInsets.all 14))
   (m/Column
    .crossAxisAlignment m.CrossAxisAlignment/start
    .children [(m/IconButton
                .onPressed (fn [] (-> m/Navigator (.of ctx) .pop))
                .icon (m/Icon m.Icons/close))
               (m/SizedBox .height 10)
               (m/Expanded
                .child
                (profile-view))])))

   
(defn widget []
  (f/widget
   (m/SafeArea)
   (m/Container)
   (default-view)))
