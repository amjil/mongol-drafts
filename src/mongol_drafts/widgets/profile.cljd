(ns mongol-drafts.widgets.profile
  (:require
   ["package:flutter/material.dart" :as m]
   [cljd.flutter :as f]
   [mongol-drafts.widgets.settings :as settings]
   [mongol-drafts.widgets.theme-dialog :as theme-dialog]
   [mongol-drafts.states.profile :as profile-state]
   [mongol-drafts.states.global :as gs]
   [mongol-drafts.states.theme :as theme-state]))
   
(defn- get-theme-subtitle [theme-mode]
  (condp = theme-mode
    theme-state/theme-mode-light "Light"
    theme-state/theme-mode-dark "Dark"
    theme-state/theme-mode-system "Follow System"
    "Not Set"))

(defn profile-view []
  (f/widget
   :context ctx
   :watch [{profile :profile} gs/state
           {current-mode :mode} theme-state/theme-state]
   :let [theme-subtitle (get-theme-subtitle current-mode)]
   (m/SingleChildScrollView .scrollDirection m.Axis/horizontal)
   (m/Row
    .crossAxisAlignment m.CrossAxisAlignment/start
    .children
    [(settings/settings-group
      [{:title "Theme"
        :leading (.palette m/Icons)
        :subtitle (str "Current: " theme-subtitle)
        :on-tap (fn [] (theme-dialog/show-theme-dialog ctx))}
       {:title "In-App Keyboard"
        :leading (.keyboard m/Icons)
        :trailing (m/RotatedBox
                   .quarterTurns -1
                   .child
                   (m/Switch
                    .value (get profile "in-app-keyboard" true)
                    .onChanged (fn [value]
                                 (profile-state/set-profile-setting
                                  {:key "in-app-keyboard"
                                   :type :bool
                                   :value value}))))}
                                 
       {:title "Sync Data"
        :leading (.sync m/Icons)
        :trailing (m/Switch
                   .value (get profile "sync-data" false)
                   .onChanged (fn [value]
                               (profile-state/set-profile-setting
                                {:key "sync-data"
                                 :type :bool
                                 :value value})))}])])))

(defn default-view []
  (f/widget
   :context ctx
   :watch [{} gs/state]
   (m/Padding 
    .padding (m/EdgeInsets.all 14))
   (m/Column
    .crossAxisAlignment m.CrossAxisAlignment/start
    .children [(m/IconButton
                .onPressed (fn [] (-> m/Navigator (.of ctx) .pop))
                .icon (m/Icon m.Icons/close))
               (m/SizedBox .height 10)
               (m/Expanded
                .child
                (profile-view))])))

   
(defn widget []
  (f/widget
   (m/SafeArea)
   (m/Container)
   (default-view)))
