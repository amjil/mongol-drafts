(ns mongol-drafts.widgets.quote.edit
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   ["package:phosphor_flutter/phosphor_flutter.dart" :as ph]
   [cljd.flutter :as f]
   [mongol-drafts.states.global :as global-state]
   [mongol-drafts.states.font :as font-state]
   [mongol-drafts.widgets.quote.state :as state]
   [virtual-keyboard.keyboard-action :as keyboard-action]))

(defn edit-section []
  (f/widget
   :context ctx
   :managed [^m/TextEditingController content-controller (m/TextEditingController. .text (or @state/content-state ""))
             ^m/TextEditingController author-controller (m/TextEditingController. .text (or @state/author-state ""))]
   :watch [{draft-font :draft-font draft-font-size :draft-font-size} font-state/font-state]
   (m/Expanded)
   (m/Padding
    .padding (m/EdgeInsets.symmetric .horizontal 16 .vertical 8))
   (m/SingleChildScrollView
    .scrollDirection m.Axis/horizontal)
   (m/Row
    .crossAxisAlignment m.CrossAxisAlignment/start
    .children
    [(m/Stack
      .children
      [(m/Positioned
        .top 0
        .left 0
        .child
        (m/Icon (ph/PhosphorIconsFill.quotes)
                .size 20
                .color (-> m/Theme (.of ctx) .-colorScheme .-onSurfaceVariant)))
       (m/Padding
        .padding (m/EdgeInsets.only .left 28)
        .child
        (mgl/MongolTextField
         .controller content-controller
         .onTap (fn []
                  (swap! global-state/state assoc :show-keyboard true)
                  (keyboard-action/clear-candidates))
         .onChanged (fn [x] (reset! state/content-state x))
         .maxLength 500
         .decoration (m/InputDecoration
                      .hintText "Quote"
                      .hintStyle (m/TextStyle .color m.Colors/grey))
         .maxLines 5
         .style (m/TextStyle .fontSize draft-font-size .height 1.8 .fontFamily draft-font)))])
     (m/SizedBox .width 24)
     (m/RotatedBox
      .quarterTurns 1
      .child
      (m/TextField
       .decoration (m/InputDecoration
                    .hintText "Author"
                    .border (m/OutlineInputBorder
                             .borderRadius (.circular m/BorderRadius 40)
                             .borderSide (m/BorderSide
                                          .width 0.2
                                          .color m.Colors/blueGrey))
                    .hintStyle (m/TextStyle .color m.Colors/grey))
       .controller author-controller
       .onChanged (fn [x] (reset! state/author-state x))
       .maxLines 1
       .style (m/TextStyle .fontSize 16)))])))
