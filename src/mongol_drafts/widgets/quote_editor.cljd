(ns mongol-drafts.widgets.quote-editor
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   ["package:image_picker/image_picker.dart" :as image-picker]
   ["package:phosphor_flutter/phosphor_flutter.dart" :as ph]
   ["dart:io" :as io]
   [cljd.flutter :as f]
   [mongol-drafts.states.global :as global-state]
   [mongol-drafts.states.font :as font-state]
   [mongol-drafts.widgets.button-bar :as button-bar]
   [virtual-keyboard.keyboard :as keyboard]
   [virtual-keyboard.keyboard-action :as keyboard-action]
   [mongol-drafts.utils.keyboard :as keyboard-utils]))

(defn- pick-image []
  (let [picker (image-picker/ImagePicker.)
        image (await (.pickImage picker .source image-picker/ImageSource.gallery))]
    (when image
      (.path image))))

(defn- take-photo []
  (let [picker (image-picker/ImagePicker.)
        image (await (.pickImage picker .source image-picker/ImageSource.camera))]
    (when image
      (.path image))))

(defn- show-image-source-dialog [ctx on-image-selected]
  (m/showDialog
   .context ctx
   .barrierColor (-> m/Colors.black (.withOpacity 0.45))
   .builder
   (fn [ctx]
     (let [theme (-> m/Theme (.of ctx))
           color-scheme (.-colorScheme theme)
           brightness (.-brightness theme)
           dialog-bg-color (if (= brightness m/Brightness.light)
                            (-> m/Colors .white)
                            (.-secondaryContainer color-scheme))]
       (m/AlertDialog
        .backgroundColor dialog-bg-color
        .content (m/Row
                .mainAxisSize m.MainAxisSize/min
                .children
                [(mgl/MongolListTile
                  .leading (m/Icon m/Icons.photo_library)
                  .title (mgl/MongolText "Select from Gallery")
                  .onTap (fn []
                           (-> m/Navigator (.of ctx) .pop)
                           (-> (pick-image)
                               (.then (fn [image-path]
                                        (when image-path
                                          (on-image-selected image-path)))))))
                 (mgl/MongolListTile
                  .leading (m/Icon m/Icons.camera_alt)
                  .title (mgl/MongolText "Take Photo")
                  .onTap (fn []
                           (-> m/Navigator (.of ctx) .pop)
                           (-> (take-photo)
                               (.then (fn [image-path]
                                        (when image-path
                                          (on-image-selected image-path)))))))
                 (mgl/MongolListTile
                  .leading (m/Icon m/Icons.delete)
                  .title (mgl/MongolText "Delete Image")
                  .onTap (fn []
                           (-> m/Navigator (.of ctx) .pop)
                           (on-image-selected nil)))])
      .actions
      [(m/TextButton
        .onPressed (fn [] (-> m/Navigator (.of ctx) .pop))
        .child (mgl/MongolText "Cancel"))])))))

(def image-path-state (atom nil))
(def preview-state (atom false))
(def content-state (atom ""))
(def author-state (atom ""))

(defn- image-section []
  (f/widget
   :context ctx
   :let [width (-> m/MediaQuery (.of ctx) .-size .-width)]
   :watch [image-path image-path-state
           preview preview-state]
   (m/Container
    .width width
    .height (if (true? preview) 160 80)
    .margin (m/EdgeInsets.all 16)
    .decoration (m/BoxDecoration
                 .color (-> m/Theme (.of ctx) .-colorScheme .-surfaceVariant)
                 .borderRadius (m/BorderRadius.circular 8))
    .child
    (m/Stack
     .children
     [(if image-path
        (m/ClipRRect
         .borderRadius (m/BorderRadius.circular 8)
         .child
         (m/Image.file
          (io/File image-path)
          .width width
          .height (if (true? preview) 160 100)
          .fit m.BoxFit/cover))
        (m/Center
         .child
         (m/Icon m/Icons.add_photo_alternate .size 48 .color (-> m/Theme (.of ctx) .-colorScheme .-onSurfaceVariant))))
      (if (true? preview)
        (m/SizedBox)
        (m/Positioned
         .top 8
         .right 8
         .child
         (m/IconButton
          .icon (m/Icon m/Icons.edit)
          .onPressed (fn []
                       (show-image-source-dialog
                        ctx
                        (fn [path]
                          (reset! image-path-state path)))))))]))))


(defn- edit-section []
  (f/widget
   :context ctx
   :managed [^m/TextEditingController content-controller (m/TextEditingController. .text (or @content-state ""))
             ^m/TextEditingController author-controller (m/TextEditingController. .text (or @author-state ""))]
   :watch [{draft-font :draft-font draft-font-size :draft-font-size} font-state/font-state]
   (m/Expanded)
   (m/Padding
    .padding (m/EdgeInsets.symmetric .horizontal 16 .vertical 8))
   (m/SingleChildScrollView
    .scrollDirection m.Axis/horizontal)
   (m/Row
    .crossAxisAlignment m.CrossAxisAlignment/start
    .children
    [(m/Stack
      .children
      [(m/Positioned
        .top 0
        .left 0
        .child
        (m/Icon (ph/PhosphorIconsFill.quotes)
                .size 20
                .color (-> m/Theme (.of ctx) .-colorScheme .-onSurfaceVariant)))
       (m/Padding
        .padding (m/EdgeInsets.only .left 28)
        .child
        (mgl/MongolTextField
         .controller content-controller
         .onTap (fn []
                  (swap! global-state/state assoc :show-keyboard true)
                  (keyboard-action/clear-candidates))
         .onChanged (fn [x] (reset! content-state x))
         .maxLength 500
         .decoration (m/InputDecoration
                      .hintText "Quote"
                      .hintStyle (m/TextStyle .color m.Colors/grey))
         .maxLines 5
         .style (m/TextStyle .fontSize draft-font-size .height 1.8 .fontFamily draft-font)))])
     (m/SizedBox .width 24)
     (m/RotatedBox
      .quarterTurns 1
      .child
      (m/TextField
       .decoration (m/InputDecoration
                    .hintText "Author"
                    .border (m/OutlineInputBorder
                             .borderRadius (.circular m/BorderRadius 40)
                             .borderSide (m/BorderSide
                                          .width 0.2
                                          .color m.Colors/blueGrey))
                    .hintStyle (m/TextStyle .color m.Colors/grey))
       .controller author-controller
       .onChanged (fn [x] (reset! author-state x))
       .maxLines 1
       .style (m/TextStyle .fontSize 16)))])))

(defn- preview-section []
  (f/widget
   :context ctx
   :watch [{draft-font :draft-font draft-font-size :draft-font-size} font-state/font-state]
   :let [;; Modern gradient colors - appealing to young people
         gradient-start (-> m/Colors.deepPurple (.withOpacity 0.9))
         gradient-end (-> m/Colors.pinkAccent (.withOpacity 0.8))
         ;; Decorative accent color
         accent-color (-> m/Colors.amberAccent (.withOpacity 0.25))
         text-color m/Colors.white
         author-color (-> m/Colors.white (.withOpacity 0.95))]
   (m/Expanded)
   (m/Center)
   (m/Padding
    .padding (m/EdgeInsets.symmetric .horizontal 20 .vertical 12)
    .child
   (m/Container
    .padding (m/EdgeInsets.all 24)
    .decoration (m/BoxDecoration
                 ;; Modern gradient background
                 .gradient (m/LinearGradient
                            .begin (m/Alignment.topLeft)
                            .end (m/Alignment.bottomRight)
                            .colors [gradient-start gradient-end])
                 ;; Soft rounded corners
                 .borderRadius (m/BorderRadius.circular 20)
                 ;; Modern shadow effects - softer and more depth
                 .boxShadow [(m/BoxShadow
                              .color (-> m/Colors.deepPurple (.withOpacity 0.3))
                              .blurRadius 20
                              .spreadRadius 2
                              .offset (m/Offset 0 8))
                             (m/BoxShadow
                              .color (-> m/Colors.pinkAccent (.withOpacity 0.2))
                              .blurRadius 15
                              .spreadRadius -2
                              .offset (m/Offset 0 -4))])
    .child
   (m/Stack
    .children
    [;; Decorative background element
     (m/Positioned
      .top -30
      .right -30
      .child
      (m/Container
       .width 150
       .height 150
       .decoration (m/BoxDecoration
                    .shape m/BoxShape.circle
                    .color accent-color)))
     ;; Main content
     (m/Column
      .mainAxisSize m.MainAxisSize/max
      .crossAxisAlignment m.CrossAxisAlignment/stretch
      .children
      [(m/Expanded
        .child
        (m/Row
         .mainAxisSize m.MainAxisSize/min
         .mainAxisAlignment m.MainAxisAlignment/center
         .children
         [(m/Column
           .crossAxisAlignment m.CrossAxisAlignment/start
           .children
           [(m/Container
             .padding (m/EdgeInsets.all 10)
             .decoration (m/BoxDecoration
                          .color (-> m/Colors.white (.withOpacity 0.2))
                          .borderRadius (m/BorderRadius.circular 10))
             .child
             (m/Icon (ph/PhosphorIconsFill.quotes)
                     .size 26
                     .color text-color))
            (m/SizedBox .height 24)
            (mgl/MongolText
             (or @content-state "Example quote")
             .style (m/TextStyle .fontSize draft-font-size
                                 .height 1.9
                                 .fontFamily draft-font
                                 .fontWeight m/FontWeight.w600
                                 .color text-color
                                 .letterSpacing 0.5))])
          (m/SizedBox .width 32)
          (m/Column
           .mainAxisAlignment m.MainAxisAlignment/end
           .crossAxisAlignment m.CrossAxisAlignment/end
           .children
           [(m/Container
             .padding (m/EdgeInsets.symmetric .horizontal 14 .vertical 8)
             .decoration (m/BoxDecoration
                          .color (-> m/Colors.white (.withOpacity 0.15))
                          .borderRadius (m/BorderRadius.circular 14))
             .child
             (m/Column
              .crossAxisAlignment m.CrossAxisAlignment/end
              .children
              [(mgl/MongolText "--"
                               .style (m/TextStyle .fontSize 14
                                                   .color author-color
                                                   .fontWeight m/FontWeight.w300))
               (m/SizedBox .height 6)
               (mgl/MongolText
                (or @author-state "Example author")
                .style (m/TextStyle .fontSize 18
                                    .color author-color
                                    .fontWeight m/FontWeight.w500
                                    .letterSpacing 0.3))]))])]))])])))))


(defn widget []
  (f/widget
   :watch [preview preview-state
           image-path image-path-state]
   (m/Scaffold)
   .body
   (m/SafeArea)
   (if preview
     (m/Column
      .children
      [(button-bar/quote-editor-bar
        preview-state
        (fn []
          (dart:core/print "share sentence >>.")))
       (if (nil? image-path)
         (m/SizedBox)
         (image-section))
       (preview-section)])
     (m/Column
      .children
      [(button-bar/quote-editor-bar
        preview-state
        (fn []
          (dart:core/print "share sentence >>.")))
       (m/Expanded
        .child
        (m/Stack
         .children
         [(m/Column
           .children
           [(image-section)
            (edit-section)])
          (keyboard/candidates (m/TextStyle. .fontSize 18 .fontFamily "OyunQaganTig"))]))
       (keyboard/keyboard {:custom-fns {:on-return keyboard-utils/handle-enter}})]))))
      

